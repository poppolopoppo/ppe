@ECHO OFF

:SETUP

SET SOLUTIONROOT=%~dp0
CD %SOLUTIONROOT%

SET FBUILDROOT=%CD%\Build
SET FBUILDCMD=%FBUILDROOT%\FBuild.exe
SET SOLUTIONPATHFILE=%FBUILDROOT%\_solution_path.bff

IF EXIST "%FBUILDCMD%" GOTO CHECKFILE

ECHO FBuild was not found in "%FBUILDROOT%"! Stopping execution.
ECHO It can be downloaded on http://fastbuild.org/docs/download.html.
SET ERRORLEVEL=127
GOTO END

:CHECKFILE
IF EXIST "%SOLUTIONPATHFILE%" GOTO RUN

:CREATEFILE
FOR /L %%x IN (10, 1, 99) DO IF EXIST "%VS110COMNTOOLS%..\..\VC\bin\10%%x\clui.dll" SET VS110CLUID=10%%x
FOR /L %%x IN (10, 1, 99) DO IF EXIST "%VS120COMNTOOLS%..\..\VC\bin\10%%x\clui.dll" SET VS120CLUID=10%%x
FOR /L %%x IN (10, 1, 99) DO IF EXIST "%VS140COMNTOOLS%..\..\VC\bin\10%%x\clui.dll" SET VS140CLUID=10%%x
ECHO Creating "%SOLUTIONPATHFILE%" ...
ECHO ; Fichier généré par fbuild.bat> "%SOLUTIONPATHFILE%"
ECHO .SolutionPath = '%CD%'>> "%SOLUTIONPATHFILE%"
ECHO .VS110CLUID = '%VS110CLUID%'>> "%SOLUTIONPATHFILE%"
ECHO .VS120CLUID = '%VS120CLUID%'>> "%SOLUTIONPATHFILE%"
ECHO .VS140CLUID = '%VS140CLUID%'>> "%SOLUTIONPATHFILE%"
ECHO Done.

:RUN
CALL "%FBUILDCMD%" %*
GOTO END

:END
IF %ERRORLEVEL% NEQ 0 EXIT /b %ERRORLEVEL%

@ECHO ON
